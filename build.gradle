buildscript {
    ext {
        springBootVersion = '2.4.5'
    }
}
plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${springBootVersion}"
}
group = 'com.flash'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_8
targetCompatibility = JavaVersion.VERSION_8

allprojects {
    // 目前Gradle版本不支持在allprojects下声明plugins，使用的是旧的写法
    apply plugin: 'java-library'

    repositories {
//        maven {
//            url "http://nexus.xyb2b.com.cn/repository/maven-public/"
//            credentials {
//                username 'xyuser'
//                password 'user123'
//            }
//        }
        mavenCentral()
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/release/' }
        maven { url "http://repo.spring.io/libs-snapshot-local" }
        maven { url "http://repo.spring.io/libs-milestone-local" }
        maven { url "http://repo.spring.io/libs-release-local" }
        jcenter()
    }

    configurations.all {
        exclude module: 'spring-boot-devtools'
        exclude module: 'spring-boot-starter-tomcat'

        resolutionStrategy {
            cacheChangingModulesFor(0, 'seconds')
        }
    }

    test {
        // 要用junit可以换成：useJUnitPlatform()
        useTestNG()
    }

    dependencies {
        api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        // 相当于provided scope，不打到构建产物中
        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

        // 相当于test scope，仅在单元测试使用
        testImplementation 'org.testng:testng:7.4.0'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // 在allprojects下只要声明lombok，junit/testng这类通用依赖
        // 不应该再添加任何属于某个子项目的依赖
    }
}